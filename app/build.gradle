apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.openlmis.core"
        minSdkVersion 17
        targetSdkVersion 21
        if (project.hasProperty("prodsim")) {
            versionCode((new Date().getTime() / 1000).toInteger())
            versionName "$prodsim"
        } else {
            versionCode androidVersionCode.toInteger()
            versionName semanticVersion
        }
    }

    signingConfigs {
        debug {
            storeFile file("../scripts/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
        release {
            storeFile file("../scripts/lmis_moz.jks")
            storePassword System.getenv("KSTOREPWD")
            keyAlias "clintonhealthaccess"
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        local {
            applicationId 'org.openlmis.core.local'
        }
        qa {
            applicationId 'org.openlmis.core.qa'
        }
        prodsim {
            applicationId 'org.openlmis.core.prodsim'
        }
        showcase {
            applicationId 'org.openlmis.core.showcase'
        }
        staging {
            applicationId 'org.openlmis.core.staging'
        }
        regression {
            applicationId 'org.openlmis.core.regression'
        }
        upgrade {
            applicationId 'org.openlmis.core.upgrade'
        }
        prd {
            applicationId 'org.openlmis.core'
        }
    }

    //the following code will change release apk file names to: {applicationId}-{versionCode}-release.apk
    //this is needed for in-app upgrade to work, because it's how the upgrade server matches package names
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk') && outputFile.name.contains("-release")) {
                def fileName = "${applicationId}-${versionCode}-release.apk"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }
}


apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = ['./src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: "testLocalDebug") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(dir: './build/intermediates/classes/local/debug/org')
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('./build/jacoco/testLocalDebug.exec')
}


apply plugin: 'checkstyle'
apply plugin: 'findbugs'


task checkstyle(type: Checkstyle) {
    configFile file("config/quality/checkstyle/checkstyle.xml")
    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/migrations/**'

    classpath = files()
}

task checkci(dependsOn: ['checkstyle', 'testLocalDebug'])

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("config/quality/findbugs/findbugs-filter.xml")
    classes = files("$project.buildDir/intermediates/classes/")

    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        html.enabled = true
        xml.enabled = false
    }

    classpath = files()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'
    compile 'com.google.android.gms:play-services-analytics:7.8.0'

    compile 'io.reactivex:rxjava:1.0.14'
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.j256.ormlite:ormlite-android:4.45'
    compile 'org.apmem.tools:layouts:1.10@aar'
    compile 'org.roboguice:roboguice:3.0.1'
    provided 'org.roboguice:roboblender:3.0.1'
    compile 'org.apache.commons:commons-lang3:3.3.2'
    provided 'org.projectlombok:lombok:1.12.6'
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true
    }
    compile 'net.danlew:android.joda:2.9.1'
    compile 'com.autoupdateapk:autoupdateapk:1.0.5'
    //this line above is from: https://github.com/cuipengfei/android-auto-updater-client, we can update source and republish it if we need to
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.assertj:assertj-core:1.7.1'
}

task bundleInstall(type: Exec) {
    workingDir "$projectDir/../functionalTests"
    executable 'bundle'
    args 'install'
}

task functionalTests(type: Exec, dependsOn: ['bundleInstall', 'assembleLocalDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-local-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @dev --format html --out reports.html"
}

task upgradeTestSetup(type: Exec, dependsOn: ['bundleInstall']) {
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    executable 'calabash-android'
    args "run", "$projectDir/../functionalTests/oldVersionApk/app-upgrade-v1.8.72-debug.apk", "--tags @upgrade_setup --format html --out upgrade_test_setup_reports.html"
}

task upgradeTests(type: Exec, dependsOn: ['bundleInstall', 'upgradeTestSetup', 'assembleUpgradeDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-upgrade-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @upgrade --format html --out upgrade_test_reports.html"
}

task regressionTests(type: Exec, dependsOn: ['bundleInstall', 'assembleRegressionDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-regression-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @regression --format html --out regression_test_reports.html"
}

task stressTests(type: Exec, dependsOn: ['bundleInstall', 'assembleRegressionDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-regression-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @STRESS_TEST --format html --out stress_test_report.html"
}

task monkeyTests(type: Exec, dependsOn: ['bundleInstall', 'assembleProdsimDebug']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if (project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-prodsim-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags @monkey --format html --out monkey_test_report.html"
}