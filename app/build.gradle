apply plugin: 'com.android.application'
apply plugin: 'io.fabric'


def buildNumber = System.getenv('BUILD_NUMBER') as Integer ?: 1

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

android {
    compileSdkVersion 21
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "org.openlmis.core"
        minSdkVersion 17
        targetSdkVersion 21
        versionCode buildNumber
        versionName "1.0.${buildNumber}"
    }

    signingConfigs {
        release {
            storeFile file("../scripts/lmis_moz.jks")
            storePassword  System.getenv("KSTOREPWD")
            keyAlias "clintonhealthaccess"
            keyPassword System.getenv("KEYPWD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        dev {
            applicationId 'org.openlmis.core.dev'
        }
        qa {
            applicationId 'org.openlmis.core.qa'
        }
        prd {
            applicationId 'org.openlmis.core'
        }
        staging {
            applicationId 'org.openlmis.core.staging'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        abortOnError false
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    sourceSets {
        androidTest {
            setRoot('src/test')
        }
    }
}


apply plugin: "jacoco"

jacoco {
    toolVersion = "0.7.1.201405082137"
}

def coverageSourceDirs = ['./src/main/java']

task jacocoTestReport(type: JacocoReport, dependsOn: "testDevDebug") {
    reports {
        xml.enabled = true
        html.enabled = true
    }
    classDirectories = fileTree(dir: './build/intermediates/classes/dev/debug/org')
    sourceDirectories = files(coverageSourceDirs)
    executionData = files('./build/jacoco/testDevDebug.exec')
}


apply plugin: 'checkstyle'
apply plugin: 'findbugs'


task checkstyle(type: Checkstyle) {
    configFile file("config/quality/checkstyle/checkstyle.xml")
    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    effort = "max"
    reportLevel = "high"
    excludeFilter = new File("config/quality/findbugs/findbugs-filter.xml")
    classes = files("$project.buildDir/intermediates/classes/")

    source 'src/main'
    include '**/*.java'
    exclude '**/gen/**'

    reports {
        html.enabled = true
        xml.enabled = false
    }

    classpath = files()
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:support-annotations:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.android.support:design:22.2.1'
    compile 'com.android.support:recyclerview-v7:22.2.1'
    compile 'com.android.support:cardview-v7:22.2.1'

    compile 'io.reactivex:rxjava:1.0.0'
    compile 'io.reactivex:rxandroid:0.24.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.0.0'
    compile 'com.squareup.okhttp:okhttp:2.0.0'

    compile 'com.j256.ormlite:ormlite-android:4.45'

    compile('org.roboguice:roboguice:3.0.1')
    provided ('org.roboguice:roboblender:3.0.1')
    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'com.astuetz:pagerslidingtabstrip:1.0.1'

    provided "org.projectlombok:lombok:1.12.6"

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.robolectric:shadows-support-v4:3.0'
    testCompile 'org.assertj:assertj-core:1.7.1'


    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.1@aar') {
        transitive = true
    }

}

task bundleInstall(type:Exec) {
    workingDir "$projectDir/../functionalTests"
    executable 'bundle'
    args 'install'
}

task functionalTests(type:Exec, dependsOn: ['bundleInstall']) {
    description 'Run Calabash Tests against specified apk (-PapkPath)'
    group JavaBasePlugin.VERIFICATION_GROUP
    workingDir "$projectDir/../functionalTests"

    def targetPath

    if(project.hasProperty("apkPath")) {
        targetPath = "$apkPath"
    } else {
        targetPath = "$projectDir/build/outputs/apk/app-dev-debug.apk"
    }

    executable 'calabash-android'
    args "run", "$targetPath", "--tags ~@pending"
}
